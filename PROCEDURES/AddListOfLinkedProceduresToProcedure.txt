
Sub AddListOfLinkedProceduresToProcedure( _
                                        Optional TargetWorkbook As Workbook, _
                                        Optional Module As VBComponent, _
                                        Optional ProcedureName As String)
'@BlogPosted
'@INCLUDE PROCEDURE RegexTest
'@INCLUDE PROCEDURE ProceduresOfWorkbook
'@INCLUDE PROCEDURE ProcedureBodyLineFirstAfterComments
'@INCLUDE PROCEDURE ActiveCodepaneWorkbook
'@INCLUDE PROCEDURE ActiveProcedure
'@INCLUDE PROCEDURE ProcedureCode
'@INCLUDE PROCEDURE ModuleOfProcedure
'@INCLUDE PROCEDURE LinkedProcedures
'@AssignedModule F_VbeLinkedProcedures

    If Not AssignCPSvariables(TargetWorkbook, Module, ProcedureName) Then Stop
    Dim Procedures As Collection
    Set Procedures = LinkedProcedures(TargetWorkbook, Module, ProcedureName)
    Dim ListOfImports As String
    Dim code As String
        code = ProcedureCode(TargetWorkbook, Module, ProcedureName)
    Dim Procedure As Variant
    For Each Procedure In Procedures
        If InStr(1, code, "@INCLUDE PROCEDURE " & Procedure) = 0 And InStr(1, ListOfImports, "@INCLUDE PROCEDURE " & Procedure) = 0 Then
            If ListOfImports = "" Then
                ListOfImports = "'@INCLUDE PROCEDURE " & Procedure
            Else
                ListOfImports = ListOfImports & vbNewLine & "'@INCLUDE PROCEDURE " & Procedure
            End If
        End If
    Next
    If ListOfImports <> "" Then
        Module.CodeModule.InsertLines ProcedureBodyLineFirstAfterComments(Module, ProcedureName), ListOfImports
    End If
End Sub
