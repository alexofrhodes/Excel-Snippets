

Sub AddListOfLinkedClassesToProcedure( _
                                     Optional TargetWorkbook As Workbook, _
                                     Optional Module As VBComponent, _
                                     Optional ProcedureName As String)
'@BlogPosted
'@INCLUDE PROCEDURE LinkedClasses
'@INCLUDE PROCEDURE ProcedureBodyLineFirstAfterComments
'@INCLUDE PROCEDURE ActiveCodepaneWorkbook
'@INCLUDE PROCEDURE ActiveProcedure
'@INCLUDE PROCEDURE ProcedureCode
'@INCLUDE PROCEDURE ModuleOfProcedure
'@AssignedModule F_VbeLinkedProcedures

    If Not AssignCPSvariables(TargetWorkbook, Module, ProcedureName) Then Stop
    Dim ListOfImports As String
    Dim code As String
        code = ProcedureCode(TargetWorkbook, Module, ProcedureName)
    Dim myClasses As Collection
    Set myClasses = LinkedClasses(TargetWorkbook, Module, ProcedureName)
    Dim element As Variant
    For Each element In myClasses
        If InStr(1, code, "@INCLUDE CLASS " & element) = 0 _
        And InStr(1, ListOfImports, "@INCLUDE CLASS " & element) = 0 Then
            If ListOfImports = "" Then
                ListOfImports = "'@INCLUDE CLASS " & element
            Else
                ListOfImports = ListOfImports & vbNewLine & "'@INCLUDE CLASS " & element
            End If
        End If
    Next
    If ListOfImports <> "" Then
        Module.CodeModule.InsertLines _
            ProcedureBodyLineFirstAfterComments(Module, ProcedureName), ListOfImports
    End If
End Sub
