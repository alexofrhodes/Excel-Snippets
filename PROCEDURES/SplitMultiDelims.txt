
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' SplitMultiDelims by alainbryden
'https://www.experts-exchange.com/articles/1480/How-to-Split-a-String-with-Multiple-Delimiters-in-VBA.html
' This function splits Text into an array of substrings, each substring
' delimited by any character in DelimChars. Only a single character
' may be a delimiter between two substrings, but DelimChars may
' contain any number of delimiter characters. It returns a single element
' array containing all of text if DelimChars is empty, or a 1 or greater
' element array if the Text is successfully split into substrings.
' If IgnoreConsecutiveDelimiters is true, empty array elements will not occur.
' If Limit greater than 0, the function will only split Text into 'Limit'
' array elements or less. The last element will contain the rest of Text.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Function SplitMultiDelims(ByVal TEXT As String, _
                        ByRef DelimChars As String, _
                        Optional ByVal IgnoreConsecutiveDelimiters As Boolean = False, _
                        Optional ByVal Limit As Long = -1) As String()
    Dim ElemStart As Long, n As Long, M As Long, Elements As Long
    Dim lDelims As Long, lText As Long
    Dim arr() As String

    lText = Len(TEXT)
    lDelims = Len(DelimChars)
    If lDelims = 0 Or lText = 0 Or Limit = 1 Then
        ReDim arr(0 To 0)
        arr(0) = TEXT
        SplitMultiDelims = arr
        Exit Function
    End If
    ReDim arr(0 To IIf(Limit = -1, lText - 1, Limit))

    Elements = 0: ElemStart = 1
    For n = 1 To lText
        If InStr(DelimChars, Mid(TEXT, n, 1)) Then
            arr(Elements) = Mid(TEXT, ElemStart, n - ElemStart)
            If IgnoreConsecutiveDelimiters Then
                If Len(arr(Elements)) > 0 Then Elements = Elements + 1
            Else
                Elements = Elements + 1
            End If
            ElemStart = n + 1
            If Elements + 1 = Limit Then Exit For
        End If
    Next n
    'Get the last token terminated by the end of the string into the array
    If ElemStart <= lText Then arr(Elements) = Mid(TEXT, ElemStart)
    'Since the end of string counts as the terminating delimiter, if the last character
    'was also a delimiter, we treat the two as consecutive, and so ignore the last elemnent
    If IgnoreConsecutiveDelimiters Then If Len(arr(Elements)) = 0 Then Elements = Elements - 1

    ReDim Preserve arr(0 To Elements) 'Chop off unused array elements
    SplitMultiDelims = arr
End Function
