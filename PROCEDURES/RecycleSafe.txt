
Public Function RecycleSafe(filespec As String, Optional ByRef ErrText As String) As Boolean
    Dim ThisWorkbookFullName As String
    Dim ThisWorkbookPath As String
    Dim WindowsFolder As String
    Dim SystemFolder As String
    Dim ProgramFiles As String
    Dim MyDocuments As String
    Dim Desktop As String
    Dim ApplicationPath As String
    Dim pos As Long
    Dim ShellObj As Object
    Dim sFileSpec As String
    Dim SHFileOp As SHFILEOPSTRUCT
    Dim Res As Long
    Dim FileNum As Integer
    sFileSpec = filespec
    If InStr(1, filespec, ":", vbBinaryCompare) = 0 Then
        RecycleSafe = False
        ErrText = "'" & filespec & "' is not a fully qualified name on the local machine"
        Exit Function
    End If
    If Dir(filespec, vbDirectory) = vbNullString Then
        RecycleSafe = False
        ErrText = "'" & filespec & "' does not exist"
        Exit Function
    End If
    If Right(sFileSpec, 1) = "\" Then
        sFileSpec = Left(sFileSpec, Len(sFileSpec) - 1)
    End If
    ThisWorkbookFullName = ThisWorkbook.FullName
    ThisWorkbookPath = ThisWorkbook.Path
    SystemFolder = String$(MAX_PATH, vbNullChar)
    GetSystemDirectory SystemFolder, Len(SystemFolder)
    SystemFolder = Left(SystemFolder, InStr(1, SystemFolder, vbNullChar, vbBinaryCompare) - 1)
    pos = InStrRev(SystemFolder, "\")
    If pos > 0 Then
        WindowsFolder = Left(SystemFolder, pos - 1)
    End If
    pos = InStr(1, Application.Path, "\", vbBinaryCompare)
    pos = InStr(pos + 1, Application.Path, "\", vbBinaryCompare)
    ProgramFiles = Left(Application.Path, pos - 1)
    ApplicationPath = Application.Path
    On Error Resume Next
    Err.clear
    Set ShellObj = CreateObject("WScript.Shell")
    If ShellObj Is Nothing Then
        RecycleSafe = False
        ErrText = "Error Creating WScript.Shell. " & CStr(Err.Number) & ": " & Err.Description
        Exit Function
    End If
    MyDocuments = ShellObj.SpecialFolders("MyDocuments")
    Desktop = ShellObj.SpecialFolders("Desktop")
    Set ShellObj = Nothing
    If (sFileSpec Like "?*:") Or (sFileSpec Like "?*:\") Then
        RecycleSafe = False
        ErrText = "File Specification is a root directory."
        Exit Function
    End If
    If (InStr(1, sFileSpec, "*", vbBinaryCompare) > 0) Or (InStr(1, sFileSpec, "?", vbBinaryCompare) > 0) Then
        RecycleSafe = False
        ErrText = "File specification contains wildcard characters"
        Exit Function
    End If
    If StrComp(sFileSpec, ThisWorkbookFullName, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is the same as this workbook."
        Exit Function
    End If
    If StrComp(sFileSpec, ThisWorkbookPath, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is this workbook's path"
        Exit Function
    End If
    If StrComp(ThisWorkbook.FullName, sFileSpec, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is this workbook."
        Exit Function
    End If
    If StrComp(sFileSpec, SystemFolder, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is the System Folder"
        Exit Function
    End If
    If StrComp(sFileSpec, WindowsFolder, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is the Windows folder"
        Exit Function
    End If
    If StrComp(sFileSpec, Application.Path, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is Application Path"
        Exit Function
    End If
    If StrComp(sFileSpec, MyDocuments, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is MyDocuments"
        Exit Function
    End If
    If StrComp(sFileSpec, Desktop, vbTextCompare) = 0 Then
        RecycleSafe = False
        ErrText = "File specification is Desktop"
        Exit Function
    End If
    If (GetAttr(sFileSpec) And vbSystem) <> 0 Then
        RecycleSafe = False
        ErrText = "File specification is a System entity"
        Exit Function
    End If
    If PathIsDirectory(sFileSpec) = 0 Then
        FileNum = FreeFile()
        On Error Resume Next
        Err.clear
        Open sFileSpec For Input Lock Read As #FileNum
        If Err.Number <> 0 Then
            Close #FileNum
            RecycleSafe = False
            ErrText = "File in use: " & CStr(Err.Number) & "  " & Err.Description
            Exit Function
        End If
        Close #FileNum
    End If
    With SHFileOp
        .wFunc = FO_DELETE
        .pFrom = sFileSpec
        .fFlags = FOF_ALLOWUNDO
        .fFlags = FOF_ALLOWUNDO + FOF_NOCONFIRMATION
    End With
    Res = SHFileOperation(SHFileOp)
    If Res = 0 Then
        RecycleSafe = True
    Else
        RecycleSafe = False
    End If
End Function
