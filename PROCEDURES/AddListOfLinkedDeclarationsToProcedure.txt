
Sub AddListOfLinkedDeclarationsToProcedure( _
                                          Optional TargetWorkbook As Workbook, _
                                          Optional Module As VBComponent, _
                                          Optional ProcedureName As String)
'@BlogPosted
'@AssignedModule F_Vbe_Declararions
'@INCLUDE PROCEDURE LinkedDeclarations
'@INCLUDE PROCEDURE ActiveCodepaneWorkbook
'@INCLUDE PROCEDURE ActiveProcedure
'@INCLUDE PROCEDURE ProcedureCode
'@INCLUDE PROCEDURE ProcedureBodyLineFirstAfterComments
'@INCLUDE PROCEDURE ModuleOfProcedure

    If ProcedureName = "" Then ProcedureName = ActiveProcedure
    If TargetWorkbook Is Nothing Then Set TargetWorkbook = ActiveCodepaneWorkbook
    Dim ListOfImports As String
    If Module Is Nothing Then Set Module = ModuleOfProcedure(TargetWorkbook, ProcedureName)
    Dim ProcedureText As String
    ProcedureText = ProcedureCode(TargetWorkbook, Module, ProcedureName)
    Dim myDeclarations As Collection
    Set myDeclarations = LinkedDeclarations(TargetWorkbook, Module, ProcedureName)
    Dim coll As New Collection
    Dim Element As Variant
    For Each Element In myDeclarations
        If InStr(1, ProcedureText, "'@INCLUDE DECLARATION " & Element) = 0 Then
            If ListOfImports = "" Then
                ListOfImports = "'@INCLUDE DECLARATION " & Element
            Else
                ListOfImports = ListOfImports & vbNewLine & "'@INCLUDE DECLARATION " & Element
            End If
        End If
    Next
    If ListOfImports <> "" Then
        Module.CodeModule.InsertLines ProcedureBodyLineFirstAfterComments(Module, ProcedureName), ListOfImports
    End If
End Sub
