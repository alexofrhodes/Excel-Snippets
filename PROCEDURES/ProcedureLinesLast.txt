
'* Modified   : Date and Time       Author              Description
'* Updated    : 17-10-2022 12:21    Alex                Added option to exclude comments after End Sub/Function (ProcedureLinesLast)

Public Function ProcedureLinesLast( _
                                  Module As VBComponent, _
                                  Procedure As String, _
                                  Optional IncludeTail As Boolean) As Long
'@BlogPosted
'@AssignedModule F_Vbe_Procedures
    Dim ProcKind As VBIDE.vbext_ProcKind
        ProcKind = vbext_pk_Proc
    Dim startAt As Long
        startAt = Module.codeModule.procStartLine(Procedure, ProcKind)
    Dim CountOf As Long
        CountOf = Module.codeModule.ProcCountLines(Procedure, ProcKind)
    Dim endAt As Long
        endAt = startAt + CountOf - 1
    If Not IncludeTail Then
        Do While Not Trim(Module.codeModule.Lines(endAt, 1)) Like "End *"
            endAt = endAt - 1
        Loop
    End If
    ProcedureLinesLast = endAt
End Function
