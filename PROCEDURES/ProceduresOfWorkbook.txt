
Function ProceduresOfWorkbook( _
                             TargetWorkbook As Workbook, _
                             Optional ExcludeDocument As Boolean = True, _
                             Optional ExcludeClass As Boolean = True, _
                             Optional ExcludeForm As Boolean = True) As Collection
'@BlogPosted
'@AssignedModule F_Vbe_Procedures
    Dim Module As VBComponent
    Dim ProcKind As VBIDE.vbext_ProcKind
    Dim lineNum As Long
    Dim coll As New Collection
    Dim ProcedureName As String
    For Each Module In TargetWorkbook.VBProject.VBComponents
        If ExcludeClass = True And Module.Type = vbext_ct_ClassModule Then GoTo SKIP
        If ExcludeDocument = True And Module.Type = vbext_ct_Document Then GoTo SKIP
        If ExcludeForm = True And Module.Type = vbext_ct_MSForm Then GoTo SKIP
        With Module.codeModule
            lineNum = .CountOfDeclarationLines + 1
            Do Until lineNum >= .CountOfLines
                ProcedureName = .ProcOfLine(lineNum, ProcKind)
                ' _ is used in events. Events may have the same name in different components
                If InStr(1, ProcedureName, "_") = 0 Then
                    coll.Add ProcedureName
                End If
                lineNum = .procStartLine(ProcedureName, ProcKind) + .ProcCountLines(ProcedureName, ProcKind) + 1
            Loop
        End With
SKIP:
    Next Module
    Set ProceduresOfWorkbook = coll
End Function
