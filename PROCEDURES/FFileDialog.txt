
Rem Files

Public Function FFileDialog( _
                            Optional ByRef lDialogType As MsoFileDialogType = msoFileDialogFilePicker, _
                            Optional sTitle As String = "", _
                            Optional sInitFileName = "", _
                            Optional bMultiSelect As Boolean = False, _
                            Optional sFilter As String = "All Files,*.*") As String
    Dim out As String
    On Error GoTo Error_Handler
    Dim oFd                   As Object
    Dim vItems                As Variant
    Dim vFilter               As Variant
    Const msoFileDialogViewDetails = 2
    Set oFd = Application.FileDialog(lDialogType)
    With oFd
        If sTitle = "" Then
            Select Case lDialogType
            Case msoFileDialogFilePicker
                .Title = "Browse for File"
            Case msoFileDialogFolderPicker
                .Title = "Browse for Folder"
            End Select
        Else
            .Title = sTitle
        End If
        If sInitFileName <> "" Then .initialFileName = sInitFileName
        .AllowMultiSelect = bMultiSelect
        .InitialView = msoFileDialogViewDetails
        If lDialogType <> msoFileDialogFolderPicker Then
            Call .Filters.clear
            For Each vFilter In Split(sFilter, "~")
                Call .Filters.Add(Split(vFilter, ",")(0), Split(vFilter, ",")(1))
            Next vFilter
        End If
        If .Show = True Then
            For Each vItems In .SelectedItems
                If out = "" Then
                    out = vItems
                Else
                    out = out & "," & vItems
                End If
            Next
        End If
    End With
    FFileDialog = out
Error_Handler_Exit:
    On Error Resume Next
    If Not oFd Is Nothing Then Set oFd = Nothing
    Exit Function
Error_Handler:
    MsgBox "The following error has occurred" & vbCrLf & vbCrLf & _
    "Error Number: " & Err.Number & vbCrLf & _
    "Error Source: fFileDialog" & vbCrLf & _
    "Error Description: " & Err.Description & _
    Switch(Erl = 0, "", Erl <> 0, vbCrLf & "Line No: " & Erl) _
    , vbOKOnly + vbCritical, "An Error has Occurred!"
    Resume Error_Handler_Exit
End Function
