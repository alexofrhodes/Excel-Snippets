
Sub AddListOfLinkedUserformsToProcedure( _
                                       Optional TargetWorkbook As Workbook, _
                                       Optional Module As VBComponent, _
                                       Optional ProcedureName As String)
'@BlogPosted
'@INCLUDE PROCEDURE ProcedureBodyLineFirstAfterComments
'@INCLUDE PROCEDURE ActiveCodepaneWorkbook
'@INCLUDE PROCEDURE ActiveProcedure
'@INCLUDE PROCEDURE ProcedureCode
'@INCLUDE PROCEDURE ModuleOfProcedure
'@INCLUDE PROCEDURE LinkedUserforms
'@AssignedModule F_VbeLinkedProcedures
    
    If ProcedureName = "" Then ProcedureName = ActiveProcedure
    If TargetWorkbook Is Nothing Then Set TargetWorkbook = ActiveCodepaneWorkbook
    Dim ListOfImports As String
    If Module Is Nothing Then Set Module = ModuleOfProcedure(TargetWorkbook, ProcedureName)
    Dim ProcedureText As String
    ProcedureText = ProcedureCode(TargetWorkbook, Module, ProcedureName)
    Dim myClasses As Collection
    Set myClasses = LinkedUserforms(TargetWorkbook, Module, ProcedureName)
    Dim Element As Variant
    For Each Element In myClasses
        If InStr(1, ProcedureText, "@INCLUDE USERFORM " & Element) = 0 And InStr(1, ListOfImports, "@INCLUDE USERFORM " & Element) = 0 Then
            If ListOfImports = "" Then
                ListOfImports = "'@INCLUDE USERFORM " & Element
            Else
                ListOfImports = ListOfImports & vbNewLine & "'@INCLUDE USERFORM " & Element
            End If
        End If
    Next
    If ListOfImports <> "" Then
        Module.CodeModule.InsertLines ProcedureBodyLineFirstAfterComments(Module, ProcedureName), ListOfImports
    End If
End Sub
