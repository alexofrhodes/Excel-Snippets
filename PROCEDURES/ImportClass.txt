
Sub ImportClass( _
                    Optional ClassName As String, _
                    Optional TargetWorkbook As Workbook, _
                    Optional Overwrite As Boolean)
'@BlogPosted
'@LastModified 2301101010

'@AssignedModule F_VbeLinkedProcedures
'@INCLUDE PROCEDURE LIBRARY_FOLDER
'@INCLUDE PROCEDURE TxtOverwrite
'@INCLUDE PROCEDURE TxtRead
'@INCLUDE PROCEDURE CheckPath
'@INCLUDE PROCEDURE TXTReadFromUrl
'@INCLUDE PROCEDURE ActiveCodepaneWorkbook
'@INCLUDE PROCEDURE ModuleAddOrSet
'@INCLUDE PROCEDURE ModuleExists
'@INCLUDE PROCEDURE CodepaneSelection
    If TargetWorkbook Is Nothing Then Set TargetWorkbook = ActiveCodepaneWorkbook
    If ClassName = "" Then ClassName = CodepaneSelection
    If ClassName = "" Or InStr(1, ClassName, " ") > 0 Then Exit Sub
    Dim filePath As String
    filePath = GITHUB_LOCAL_LIBRARY_CLASSES & ClassName & ".cls"
    If CheckPath(filePath) = "I" Then
        On Error Resume Next
        Dim code As String
        code = TXTReadFromUrl(GITHUB_LIBRARY_CLASSES & ClassName & ".cls")
        On Error GoTo 0
        If Len(code) > 0 And Not UCase(code) Like ("*NOT FOUND*") Then
            TxtOverwrite filePath, code
        Else
            MsgBox "File " & ClassName & ".cls not found neither localy nor online"
            Exit Sub
        End If
    End If

    If ModuleExists(ClassName, TargetWorkbook) Then
        If Overwrite = True Then
            TargetWorkbook.VBProject.VBComponents.Remove TargetWorkbook.VBProject.VBComponents(ClassName)
        Else
            Exit Sub
        End If
    End If
    TargetWorkbook.VBProject.VBComponents.Import filePath
End Sub
